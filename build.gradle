import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'com.github.spotbugs' version '5.0.13'
}

ext {
    versions = [:]
    versions.spring = System.getenv("SPRING") == '5' ? '5.3.24' : '6.0.3'
    versions.springApi = System.getenv("SPRING") == '5' ? '5.1.0.RELEASE' : '6.0.0'
    versions.target = System.getenv("SPRING") == '5' ? 8 : 17
    versions.junit = '5.9.1'
}

version = "${majorVersion}.${minorVersion}.${patchVersion}"

// dependencies

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api "org.springframework:spring-context:${versions.springApi}"
    implementation "org.springframework:spring-context:${versions.spring}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
            "org.springframework:spring-test:${versions.spring}",
            'org.hamcrest:hamcrest:2.2',
            'org.mockito:mockito-junit-jupiter:4.10.0'
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${versions.junit}"
}

// compiling

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.toVersion(versions.target)
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile) {
    configure(options) {
        encoding = 'UTF-8'
        deprecation = true
        compilerArgs << "-Xlint:unchecked"
    }
}

// unit tests

test {
    useJUnitPlatform()
}

// publishing

javadoc {
    options.memberLevel = 'PACKAGE'
    options.author = false
    options.source = '1.8'
    options.header = "${project.description} ${project.version}"
    options.links(["https://docs.oracle.com/javase/8/docs/api/",
                   "https://docs.spring.io/spring-framework/docs/${versions.spring}/javadoc-api/"] as String[])
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.withType(Jar) {
    from(project.projectDir) {
        include 'LICENSE'
        into('META-INF')
    }
    manifest {
        attributes(
                "Automatic-Module-Name": "diergo.spring.legacy",
                "Specification-Title": project.description,
                "Specification-Version": "${majorVersion}.${minorVersion}",
                "Specification-Vendor": vendor,
                "Implementation-Title": project.description,
                "Implementation-Version": "${majorVersion}.${minorVersion}.${patchVersion}",
                "Implementation-Vendor": vendor,
                "Implementation-Vendor-Id": project.group
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                description = project.description
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}

// code analysis

tasks.withType(SpotBugsTask) {
    reports {
        xml.required = false
        html.required = true
    }
}

jacocoTestReport {
    reports {
        html.required = true
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                counter = 'CLASS'
                minimum = 0.9
            }
            limit {
                counter = 'LINE'
                minimum = 0.8
            }
            limit {
                counter = 'BRANCH'
                minimum = 0.7
            }
        }
    }
}

// task dependencies

check.dependsOn jacocoTestCoverageVerification, jacocoTestReport
assemble.dependsOn javadoc

wrapper {
    gradleVersion = '7.6'
}
